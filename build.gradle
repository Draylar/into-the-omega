plugins {
    id 'fabric-loom' version '0.7-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version + "-" + project.minecraft_version
group = project.maven_group

minecraft {
    accessWidener "src/main/resources/intotheomega.accesswidener"
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url "http://server.bbkr.space:8081/artifactory/libs-release" }

    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }

    maven {
        url 'https://repo.repsy.io/mvn/gandiber/geckolib'
    }

    maven {
        name = "dblsaiko"
        url = "https://maven.dblsaiko.net/"
    }

    maven { url = "https://hephaestus.dev/release" }
    maven { url "https://maven.shedaniel.me/" }
    mavenLocal()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
    modCompile ("net.fabricmc.fabric-api:fabric-rendering-v1:1.5.1+ca58154a7d")
    modCompile ("net.fabricmc.fabric-api:fabric-api:0.29.4+1.16") { // do not update this fabric version or EVERYTHING WILL BREAK
        exclude(module: "fabric-rendering-v1")
    }

    // gecko lib
    modImplementation ("software.bernie.geckolib:geckolib-fabric-1.16.5:3.0.31:dev") {
        exclude group: "net.fabricmc.fabric-api"
    }
    include "software.bernie.geckolib:geckolib-fabric-1.16.5:3.0.31:dev"

    // trinkets
    modImplementation ("com.github.emilyploszaj:trinkets:${trinkets_version}") {
        exclude group: "io.github.onyxstudios.Cardinal-Components-API"
        exclude group: "net.fabricmc.fabric-api"
    }
    include "com.github.emilyploszaj:trinkets:${trinkets_version}"

    //  shield lib
    modImplementation ("com.github.CrimsonDawn45:Fabric-Shield-Lib:${project.fabric_shield_lib_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    include "com.github.CrimsonDawn45:Fabric-Shield-Lib:${project.fabric_shield_lib_version}"

    // REI
    modImplementation ("me.shedaniel:RoughlyEnoughItems:5.2.10") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // CCA
    modImplementation ("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:2.8.2") {
        exclude group: "net.fabricmc.fabric-api"
    }
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:2.8.2"
    modImplementation ("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:2.8.2") {
        exclude group: "net.fabricmc.fabric-api"
    }
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:2.8.2"

    // Attributed
    modImplementation ('com.github.Draylar:attributed:1.0.4.1-1.16.5') {
        exclude group: "net.fabricmc.fabric-api"
    }
    include ('com.github.Draylar:attributed:1.0.4.1-1.16.5') {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Enderman spawner patch
    modCompile ('com.github.Draylar:angerable-patch:1.0.0') {
        exclude group: "net.fabricmc.fabric-api"
    }
    include 'com.github.Draylar:angerable-patch:1.0.0'

    // Dev building utils
    modImplementation ('com.github.Draylar:staff-of-building:1.2.4-1.16.2') {
        exclude group: "net.fabricmc.fabric-api"
    }

    // cool rendering
    modCompileOnly ("grondag:canvas-mc116-configurable-pipeline:1.0.+") {
        exclude group: "com.terraformersmc"
        exclude group: "net.fabricmc.fabric-api"
        exclude group: "io.github.prospector"
    }

    modImplementation ("dev.monarkhes:myron:1.5.2") {
        exclude group: "net.fabricmc.fabric-api"
    }
    include "dev.monarkhes:myron:1.5.2"

    modCompileOnly ('com.github.Draylar:visible-structure-void:b4ad15c4d7') {
        exclude group: "net.fabricmc.fabric-api"
    }
    modRuntime ('com.github.Draylar:visible-structure-void:b4ad15c4d7') {
        exclude group: "net.fabricmc.fabric-api"
    }

    include 'draylar:jvoronoi:1.0.2'
    modImplementation ('draylar:jvoronoi:1.0.2') {
        exclude group: "org.openjfx"
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
